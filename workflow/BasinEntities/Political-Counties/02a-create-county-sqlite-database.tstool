StartLog(LogFile="02a-create-county-sqlite-database.tstool.log",MaxSize="100000000")
# Create the SQLite database of county population data.
# This can be used to query data and convert into time series.
#
# Read the population estimate data file downloaded from DOLA
# - need to adjust some strings to deal with inconsistencies
# - "City Of Castle Pines" replaced with "City of Castle Pines" because mixture causes problems sorting
ReadTableFromDelimitedFile(TableID="CountyPopulationEstimates_DOLA_Table",InputFile="downloads/population-estimate-data.csv",HeaderLines="1")
ManipulateTableString(TableID="CountyPopulationEstimates_DOLA_Table",InputColumn1="municipalityname",Operator="Replace",InputValue2="City Of Castle Pines",InputValue3="City of Castle Pines",OutputColumn="municipalityname")
SortTable(TableID="CountyPopulationEstimates_DOLA_Table",SortColumns="municipalityname,year")
#
# Create an in-memory SQLite database containing county population data.
# - totalpopulation has some missing in the csv file so don't require NOT NULL
# - placefips has some missing in the csv file so don't require NOT NULL
NewSQLiteDatabase(DataStore="CountyDatabase",DatabaseFile="Memory")
# NewSQLiteDatabase(DataStore="CountyDatabase",DatabaseFile="db/counties.db")
RunSql(DataStore="CountyDatabase",Sql="CREATE TABLE IF NOT EXISTS county_pop_estimate_dola (id integer, countyfips integer NOT NULL, placefips integer, municipalityname TEXT NOT NULL, year integer NOT NULL, totalpopulation integer, countyplace integer NOT NULL);")
WriteTableToDataStore(TableID="CountyPopulationEstimates_DOLA_Table",DataStore="CountyDatabase",DataStoreTable="county_pop_estimate_dola",WriteMode=InsertUpdate)
# Use the following to compare original counties tables read from csv and database table
ReadTableFromDataStore(DataStore="CountyDatabase",DataStoreTable="county_pop_estimate_dola",OrderBy="municipalityname COLLATE NOCASE,year",TableID="test")
CompareTables(Table1ID="CountyPopulationEstimates_DOLA_Table",Table2ID="test",NewTableID="ComparisonTable",OutputFile="ComparisonTable.html",IfDifferent=Warn)
#===================================================================
#
# Process the county data into time series
# - first get the unique list of counties
ReadTableFromDataStore(DataStore="CountyDatabase",Sql="SELECT DISTINCT municipalityname, countyfips FROM county_pop_estimate_dola WHERE municipalityname LIKE '%COUNTY%' ORDER BY municipalityname",TableID="CountyListTable")
ManipulateTableString(TableID="CountyListTable",InputColumn1="municipalityname",Operator="Remove",InputValue2="\sCOUNTY",OutputColumn="county")
#
For(Name="For_Counties",IteratorProperty="County",TableID="CountyListTable",TableColumn="county")
# First get the data records from the database.
ReadTableFromDataStore(DataStore="CountyDatabase",Sql="SELECT * FROM county_pop_estimate_dola WHERE municipalityname = '${County} COUNTY' ORDER BY year",TableID="${County}_County_Data_Table")
# Convert the data records to a time series.
TableToTimeSeries(TableID="${County}_County_Data_Table",DateColumn="year",LocationID="${County}",LocationType="County",ValueColumn="totalpopulation",DataSource="DOLA",DataType="Population",Interval=Year,Units="persons",Alias="%L-%T-%I")
EndFor(Name="For_Counties")
#===================================================================
#
# Process the county unincorporated area data into time series
For(Name="For_Counties_Unincorp",IteratorProperty="County",TableID="CountyListTable",TableColumn="county")
# First get the data records from the database.
ReadTableFromDataStore(DataStore="CountyDatabase",Sql="SELECT * FROM county_pop_estimate_dola WHERE UPPER(municipalityname) = '${County} UNINCORP. AREA' ORDER BY year",TableID="${County}_County_Unincorported_Data_Table")
# Convert the data records to a time series.
TableToTimeSeries(TableID="${County}_County_Unincorported_Data_Table",DateColumn="year",LocationID="${County}",LocationType="CountyUnincorporated",ValueColumn="totalpopulation",DataSource="DOLA",DataType="Population",Interval=Year,Units="persons",Alias="%L_Unincorporated-%T-%I")
EndFor(Name="For_Counties_Unincorp")
#===================================================================
#
# Process the county muni area data into time series
# - all non-county and non-total time series
For(Name="For_Counties_Muni",IteratorProperty="CountyFIPS",TableID="CountyListTable",TableColumn="countyfips",TablePropertyMap="county:County")
# First get the data records from the database.
ReadTableFromDataStore(DataStore="CountyDatabase",Sql="SELECT * FROM county_pop_estimate_dola WHERE ((countyfips = ${CountyFIPS}) AND (municipalityname NOT LIKE '%COUNTY%') AND (municipalityname NOT LIKE '%(Total)%') AND (municipalityname NOT LIKE '%Unincorp%')) ORDER BY year",TableID="${County}_County_Muni_Data_Table")
# Convert the data records to a time series.
TableToTimeSeries(TableID="${County}_County_Muni_Data_Table",DateColumn="year",LocationID="${County}",LocationType="CountyMuni",ValueColumn="totalpopulation",DataSource="DOLA",DataType="Population",Interval=Year,Units="persons",HandleDuplicatesHow=Add,Alias="%L_Muni-%T-%I")
EndFor(Name="For_Counties_Muni")
#===================================================================
#
# Process municipality data into time series
# - counties have placefips=0 and unincorporated areas have 'Unincorp' in municipalityname
# - some municipalities are in more than one county and have '(Total)' in the name, so remove
# - some municipalities have the same name as counties so filter out
ReadTableFromDataStore(DataStore="CountyDatabase",Sql="SELECT DISTINCT municipalityname FROM county_pop_estimate_dola WHERE ((placefips != 0) AND (municipalityname NOT LIKE '%Unincorp%') AND (municipalityname NOT LIKE '%Part%') AND (municipalityname NOT LIKE '%COUNTY%')) ORDER BY municipalityname",TableID="MunicipalityListTable")
ManipulateTableString(TableID="MunicipalityListTable",InputColumn1="municipalityname",Operator="Remove",InputValue2="\s(Total)",OutputColumn="dola_municipality")
#
For(Name="For_Municipalities",IteratorProperty="Municipality",TableID="MunicipalityListTable",TableColumn="dola_municipality")
# First get the data records from the database.
ReadTableFromDataStore(DataStore="CountyDatabase",Sql="SELECT * FROM county_pop_estimate_dola WHERE ((municipalityname LIKE '%${Municipality}%') AND (municipalityname NOT LIKE '%Part%') AND (municipalityname NOT LIKE '%COUNTY%') AND (municipalityname NOT LIKE '%Unincorp%')) ORDER BY year",TableID="${Municipality}_Municipality_Data_Table")
# Convert the data records to a time series.
TableToTimeSeries(TableID="${Municipality}_Municipality_Data_Table",DateColumn="year",LocationID="${Municipality}",LocationType="Municipality",ValueColumn="totalpopulation",DataSource="DOLA",DataType="Population",Interval=Year,Units="persons",Alias="%L-Municipality-%T-%I")
EndFor(Name="For_Municipalities")
